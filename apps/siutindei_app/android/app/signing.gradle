import java.util.Properties

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")

if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def storeFilePath = System.getenv("KEYSTORE_PATH")
if (!storeFilePath && keystorePropertiesFile.exists()) {
    storeFilePath = keystoreProperties["storeFile"]
}

def storePasswordValue = System.getenv("KEYSTORE_PASSWORD") ?: keystoreProperties["storePassword"] ?: ""
def keyAliasValue = System.getenv("KEY_ALIAS") ?: keystoreProperties["keyAlias"] ?: ""
def keyPasswordValue = System.getenv("KEY_PASSWORD") ?: keystoreProperties["keyPassword"] ?: ""

def releaseKeystoreFile = storeFilePath ? rootProject.file(storeFilePath) : null
def hasReleaseKeystore = releaseKeystoreFile != null && releaseKeystoreFile.exists()
def hasReleaseCredentials = storePasswordValue && keyAliasValue && keyPasswordValue
def storeTypeValue = System.getenv("KEYSTORE_TYPE") ?: keystoreProperties["storeType"]
if (!storeTypeValue && releaseKeystoreFile != null) {
    def lowerName = releaseKeystoreFile.name.toLowerCase()
    if (lowerName.endsWith(".p12") || lowerName.endsWith(".pfx")) {
        storeTypeValue = "PKCS12"
    }
}

if (hasReleaseKeystore && !hasReleaseCredentials) {
    throw new GradleException("Release keystore found but signing credentials are missing.")
}

android {
    signingConfigs {
        if (hasReleaseKeystore && hasReleaseCredentials) {
            release {
                storeFile releaseKeystoreFile
                if (storeTypeValue) {
                    storeType storeTypeValue
                }
                storePassword storePasswordValue
                keyAlias keyAliasValue
                keyPassword keyPasswordValue
            }
        }
    }

    buildTypes {
        release {
            if (hasReleaseKeystore && hasReleaseCredentials) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
    }
}
